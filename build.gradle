buildscript {
    ext.kotlin_version = '1.3.72'
    ext.spring_boot_version = '2.2.6.RELEASE'
    ext.opensaml_version = '3.4.5'
    ext.detekt_version = '1.8.0'
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
    }
}

group = 'gdsace'

apply plugin: 'kotlin'
apply plugin: 'kotlin-spring'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.gitlab.arturbosch.detekt'

jar {
    archiveBaseName = 'svc-auth'
}

detekt {
    buildUponDefaultConfig = true
    config = files("$projectDir/detekt-config.yml")

    reports {
        html.enabled = true // observe findings in your browser with structure and code snippets
        xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
    }
}

configurations {
    ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}
check.dependsOn ktlint

task ktlintFix(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "-F", "src/**/*.kt"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://build.shibboleth.net/nexus/content/repositories/releases" }
}

sourceCompatibility = 14
targetCompatibility = 14

dependencies {
    implementation("org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version")
    implementation("org.springframework.boot:spring-boot-starter-web:$spring_boot_version")
    implementation("org.jetbrains.kotlin:kotlin-reflect:$kotlin_version")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf:$spring_boot_version")
    implementation('com.google.guava:guava:29.0-jre')
    implementation('io.opentracing.contrib:opentracing-spring-web-autoconfigure:0.3.2')
    implementation('io.opentracing.brave:brave-opentracing:0.36.2')
    implementation('io.zipkin.reporter2:zipkin-sender-okhttp3:2.12.3')
    implementation("org.springframework.boot:spring-boot-starter-actuator:$spring_boot_version")
    implementation("io.micrometer:micrometer-registry-prometheus:1.3.8")
    implementation('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0')
    implementation('org.bitbucket.b_c:jose4j:0.7.0')

    implementation("org.opensaml:opensaml-core:$opensaml_version")
    implementation("org.opensaml:opensaml-saml-api:$opensaml_version")
    implementation("org.opensaml:opensaml-saml-impl:$opensaml_version")
    implementation("org.opensaml:opensaml-messaging-api:$opensaml_version")
    implementation("org.opensaml:opensaml-messaging-impl:$opensaml_version")
    implementation("org.opensaml:opensaml-soap-api:$opensaml_version")
    implementation("org.opensaml:opensaml-soap-impl:$opensaml_version")

    implementation('javax.xml.bind:jaxb-api:2.3.1')
    implementation('com.sun.xml.bind:jaxb-core:2.3.0.1')
    implementation('com.sun.xml.bind:jaxb-impl:2.3.2')

    // dependencies of opensaml with vulnerabilites, so update them manually
    implementation('org.bouncycastle:bcprov-jdk15on:1.65')
    implementation('commons-collections:commons-collections:3.2.2')
    implementation('org.apache.santuario:xmlsec:2.1.5')
    implementation('commons-codec:commons-codec:1.14')
    implementation('org.cryptacular:cryptacular:1.2.4')


    testImplementation("org.springframework.boot:spring-boot-starter-test:$spring_boot_version")
    testImplementation('io.mockk:mockk:1.10.0')

    ktlint("com.github.shyiko:ktlint:0.31.0")
}
test {
    reports {
        html.enabled = true
    }
}